defmodule Zipper do
  @type t() :: Zipper

  defstruct tree: nil, back: nil

  @doc """
  Get a zipper focused on the root node.
  """
  @spec from_tree(BinTree.t()) :: Zipper.t()
  def from_tree(bin_tree) do
    from_tree(bin_tree, nil)
  end

  defp from_tree(bin_tree, back) do
    %Zipper{tree: bin_tree, back: back}
  end

  @doc """
  Get the complete tree from a zipper.
  """
  @spec to_tree(Zipper.t()) :: BinTree.t()
  def to_tree(zipper) do
    case zipper.back do
      nil -> zipper.tree
      back -> to_tree(back.(zipper.tree))
    end
  end

  @doc """
  Get the value of the focus node.
  """
  @spec value(Zipper.t()) :: any
  def value(zipper) do
    zipper.tree.value
  end

  @doc """
  Get the left child of the focus node, if any.
  """
  @spec left(Zipper.t()) :: Zipper.t() | nil
  def left(zipper) do
    case zipper.tree.left do
      nil -> nil
      left -> from_tree(left, &set_left(zipper, &1))
    end
  end

  @doc """
  Get the right child of the focus node, if any.
  """
  @spec right(Zipper.t()) :: Zipper.t() | nil
  def right(zipper) do
    case zipper.tree.right do
      nil -> nil
      right -> from_tree(right, &set_right(zipper, &1))
    end
  end

  @doc """
  Get the parent of the focus node, if any.
  """
  @spec up(Zipper.t()) :: Zipper.t() | nil
  def up(zipper) do
    case zipper.back do
      nil -> nil
      back -> back.(zipper.tree)
    end
  end

  @doc """
  Set the value of the focus node.
  """
  @spec set_value(Zipper.t(), any) :: Zipper.t()
  def set_value(zipper, value) do
    %{zipper | tree: %{zipper.tree | value: value}}
  end

  @doc """
  Replace the left child tree of the focus node.
  """
  @spec set_left(Zipper.t(), BinTree.t() | nil) :: Zipper.t()
  def set_left(zipper, left) do
    %{zipper | tree: %{zipper.tree | left: left}}
  end

  @doc """
  Replace the right child tree of the focus node.
  """
  @spec set_right(Zipper.t(), BinTree.t() | nil) :: Zipper.t()
  def set_right(zipper, right) do
    %{zipper | tree: %{zipper.tree | right: right}}
  end
end
